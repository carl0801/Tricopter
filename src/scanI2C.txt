#include <Arduino.h>
#include <Wire.h>

// Set I2C bus to use: Wire, Wire1, etc.
#define WIRE Wire

void setup() {
    WIRE.begin();
    Serial.begin(9600);
    while (!Serial) delay(10); // Wait for Serial connection
    Serial.println("\nI2C Scanner");
}

void loop() {
    uint8_t error, address; // Use uint8_t instead of byte

    int nDevices = 0;

    Serial.println("Scanning...");

    for (address = 1; address < 127; address++) {
        // The i2c_scanner uses the return value of
        // the Wire.endTransmission to see if
        // a device did acknowledge to the address.
        WIRE.beginTransmission(address);
        error = WIRE.endTransmission();

        if (error == 0) {
            Serial.print("I2C device found at address 0x");
            if (address < 16) Serial.print("0");
            Serial.print(address, HEX);
            Serial.println("!");
            nDevices++;
        } else if (error == 4) {
            Serial.print("Unknown error at address 0x");
            if (address < 16) Serial.print("0");
            Serial.println(address, HEX);
        }
    }

    if (nDevices == 0)
        Serial.println("No I2C devices found\n");
    else
        Serial.println("done\n");

    delay(5000); // Wait 5 seconds for the next scan
}
